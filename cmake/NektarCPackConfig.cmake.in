if(CPACK_GENERATOR STREQUAL "DEB")
    # Debian packages
    execute_process(COMMAND dpkg --print-architecture 
        OUTPUT_VARIABLE CPACK_DEBIAN_PACKAGE_ARCHITECTURE
        OUTPUT_STRIP_TRAILING_WHITESPACE)
    set(ARCHITECTURE ${CPACK_DEBIAN_PACKAGE_ARCHITECTURE})
elseif(CPACK_GENERATOR STREQUAL "RPM")
    execute_process(COMMAND rpm --eval %{_arch}
                OUTPUT_VARIABLE CPACK_RPM_PACKAGE_ARCHITECTURE
                OUTPUT_STRIP_TRAILING_WHITESPACE)
    set(ARCHITECTURE ${CPACK_RPM_PACKAGE_ARCHITECTURE})

    set(CPACK_PACKAGE_FILE_NAME
        "nektar-${CPACK_PACKAGE_VERSION}-1.${CPACK_RPM_PACKAGE_ARCHITECTURE}")
    set(CPACK_RPM_EXCLUDE_FROM_AUTO_FILELIST /usr /usr/bin /usr/lib64 /usr/lib64/openmpi
        /usr/lib64/openmpi/lib /usr/lib64/openmpi/bin)
elseif(CPACK_GENERATOR STREQUAL "PackageMaker")
    # OS X .pkg installer -- set install location
    set(CPACK_MONOLITHIC_INSTALL ON)

    # Set install locations
    set(CPACK_PACKAGE_DEFAULT_LOCATION "/")
    set(CPACK_PACKAGING_INSTALL_PREFIX "/opt/nektar++")
    set(CPACK_RESOURCE_FILE_WELCOME "@CMAKE_SOURCE_DIR@/pkg/osx-installer.html")
    set(CPACK_POSTFLIGHT_SCRIPT "@CMAKE_SOURCE_DIR@/pkg/osx-postflight.sh")

    # This is used as part of the identifier, so make it without spaces
    set(CPACK_PACKAGE_VENDOR "imperialcollegelondon")
elseif(CPACK_GENERATOR STREQUAL "TGZ" OR
       CPACK_GENERATOR STREQUAL "TXZ" OR
       CPACK_GENERATOR STREQUAL "TZ" OR
       CPACK_GENERATOR STREQUAL "TBZ2" OR
       CPACK_GENERATOR STREQUAL "ZIP")
    # Archive generators should use a monolithic installation
    set(CPACK_MONOLITHIC_INSTALL ON)
endif()

# Determine architecture if not detected by rpm/dpkg above
if(NOT DEFINED ARCHITECTURE)
    execute_process(COMMAND uname -m 
        OUTPUT_VARIABLE ARCHITECTURE
        OUTPUT_STRIP_TRAILING_WHITESPACE)
endif()
